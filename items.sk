on right click:
  set {_n} to uncolored name of player's tool
  set {_l} to uncolored lore of player's tool
     
#
# Potions
#
     
  if {_l} contains "restores":
    set {_split::*} to {_l} split at " "
    set {_a} to {_split::2} parsed as integer
    if {_split::3} is "HP":
      if player's gamemode is not creative:
        remove 1 of player's tool lored lore of player's tool from player
        add {_a} to player's health
        execute console command "/execute at %player% run playsound entity.generic.drink master @a"
    if {_split::3} is "MP":
      if player's gamemode is not creative:
        remove 1 of player's tool lored lore of player's tool from player
        add {_a} to {mana::%player's uuid%}
        if {mana::%player's uuid%} > {maxmana::%player's uuid%}:
          set {mana::%player's uuid%} to {maxmana::%player's uuid%}
        execute console command "/execute at %player% run playsound entity.generic.drink master @a"
     
#
# Weapons
#
     
  if {_n} is "Blowpipe":
    if player has 1 wheat seeds named "&fSeeds" lored "&7Ammo":
      remove 1 wheat seeds named "&fSeeds" lored "&7Ammo" from player
      play sound "block.dispenser.launch" at volume 1 and pitch 2 at location of player
      set {_l} to location 2 above player
      set {_loc} to lineCheck(player, 1)
      drawLine particle redstone, RGB 173, 235, 173, center {_l}, target {_loc}, id "%player%", solid true, density 10, visibleRange 32
      wait 1 tick
      stopEffect id "%player%"
     
  if {_n} is "Smoke Cloud":
    if spellCheck(player, 10) = 1:
      stopEffect id "%player%"
      set {_loc} to location of block at player
      play sound "entity.blaze.shoot" at volume 1 and pitch 0.5 at location of player
      drawSphere style 1, particle "smoke", center {_loc}, id "%player%", rM false, radius 3, density 25, visibleRange 32, disXYZ 0, 0, 0, pulseDelay 1
      loop 8 times:
        set {_t::*} to (all entities) where [sphereCheck(input, {_loc}, 3.5, 1) = ""]
        wait 1 second
      stopEffect id "%player%"
      endCheck(player)
   
  if {_n} is "Starbeam":
    if spellCheck(player, 10) = 1:
      set {_l} to location 2 above player
      set {_loc} to lineCheck(player, 4.5)
      play sound "entity.blaze.shoot" at volume 1 and pitch 2 at location of player
      drawLine particle redstone, RGB 255, 51, 204, center {_l}, target {_loc}, id "%player%", solid true, density 10, visibleRange 32
      wait 1 tick
      stopEffect id "%player%"
      endCheck(player)

  if {_n} is "Wrath of the Departed":
    if spellCheck(player, 15) = 1:
      play sound "item.totem.use" at volume 1 and pitch 2 at location of player
      drawDot count 100, particle "fireworkspark", speed 1, XYZ 0.3, 0.3, 0.3, center location of player, visibleRange 32, pulseDelay 0, keepFor 1 tick
      set {_t::*} to (all entities) where [sphereCheck(input, location of block at location of player, 4, 12) = ""]
      wait 1 tick
      endCheck(player)

#
# Specials
#

on right click on bat:
  if uncolored name of player's tool contains "Spirit Bottle":
    if {level::%player's uuid%} is not set:
      set {level::%player's uuid%} to 1
    set {_n} to uncolored name of event-entity
    if player's gamemode is not creative:
      remove 1 of player's tool from player
    if {_n} contains "Sprite":
      set {_i} to potion named "&fPixie in a Bottle"
      add "{CustomPotionColor:7929855}" to nbt of {_i}
    if {_n} contains "Wraith":
      set {_i} to potion named "&eWraith in a Bottle"
      add "{CustomPotionColor:16777080}" to nbt of {_i}
    if {_n} contains "Phantasm":
      set {_i} to potion named "&dPhantasm in a Bottle"
      add "{CustomPotionColor:16742655}" to nbt of {_i}
    if {_n} contains "Eidolon":
      set {_i} to potion named "&aEidolon in a Bottle"
      add "{CustomPotionColor:7929720}" to nbt of {_i}
    if {_i} is set:
      play sound "item.bottle.fill_dragonbreath" at volume 1 and pitch 1.5 at location of player
      add "{HideFlags:63}" to nbt of {_i}
      give player {_i} lored "&7Material||&7It glows with a||&7strange energy.."
      teleport event-entity to location(0, -100, 0, world("ul_prox"))

on consume:
  cancel event


on step on cracked stone bricks:
  if uncolored name of player's boots is "Weighted Boots":
    if block at location of event-block is not air:
      set event-block to air
      play sound "block.stone.break" at volume 1 and pitch 0.5 at location of event-block
      drawDot count 10, particle "blockcrack", material cracked stone bricks, speed 0.2, XYZ 0.3, 0.3, 0.3, center location of event-block, visibleRange 32, pulseDelay 0, keepFor 1 tick
      wait 5 seconds
      set event-block to cracked stone bricks
      play sound "block.stone.place" at volume 1 and pitch 1 at location of event-block
      drawDot count 5, particle "smoke", speed 0.02, XYZ 0.3, 0.3, 0.3, center location of event-block, visibleRange 32, pulseDelay 0, keepFor 1 tick
